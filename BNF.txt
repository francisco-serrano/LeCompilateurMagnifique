<programa> ::== <bloque_sentencias>

<funcion> ::== <tipo> FUNCTION ID <cuerpo_funcion>
<funcion> ::== <tipo> MOVE FUNCTION ID <cuerpo_funcion>

<tipo> ::== UINT
<tipo> ::== ULONG

<cuerpo_funcion> ::== 
	OPEN_BRACE 
		<bloque_sentencias>
		RETURN OPEN_PAR <expresion> CLOSE_PAR DOT 
	CLOSE_BRACE

<cuerpo_funcion> ::==
	OPEN_BRACE
		RETURN OPEN_PAR <expresion> CLOSE_PAR DOT
	CLOSE_BRACE

<invocacion_funcion> ::== ID OPEN_PAR CLOSE_PAR

<declaracion> ::== <lista_variables> COLON <tipo> DOT
<declaracion> ::== <funcion>
<lista_variables> ::== ID COMMA <lista_variables>
<lista_variables> ::== ID

<ejecutable> ::== IF OPEN_PAR <condicion> CLOSE_PAR THEN <bloque_sentencias> ELSE <bloque_sentencias> END_IF
<ejecutable> ::== IF OPEN_PAR <condicion> CLOSE_PAR THEN <bloque_sentencias> END_IF
<ejecutable> ::== OUT OPEN_PAR CADENA CLOSE_PAR DOT

<control> ::== WHILE OPEN_PAR <condicion> CLOSE_PAR DO <bloque_sentencias> DOT

<condicion> ::== <expresion> <comparador> <expresion>

<expresion> ::== <invocacion_funcion>
<expresion> ::== <expresion> ADD <termino>
<expresion> ::== <expresion> SUB <termino>
<expresion> ::== <termino>

<termino> ::== <termino> MULT <factor>
<termino> ::== <termino> DIV <factor>
<termino> ::== <factor>

<factor> ::== ID
<factor> ::== CTE

<comparador> ::== LEQ | GEQ | LT | GT | EQ | NEQ

<asignacion> ::== ID ASIGN <expresion> DOT

<bloque_sentencias> ::== <bloque_compuesto>
<bloque_sentencias> ::== <bloque_simple>

<bloque_compuesto> ::== BEGIN <sentencias> END
<bloque_simple> ::== <sentencia>

<sentencias> ::== <sentencia> <sentencias>
<sentencias> ::== <sentencia>

<sentencia> ::== <asignacion>
<sentencia> ::== <declaracion>
<sentencia> ::== <ejecutable>
<sentencia> ::== <control>
